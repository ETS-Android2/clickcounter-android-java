buildscript {
    repositories {
        gradlePluginPortal()
    }
    dependencies {
        classpath 'com.hiya:jacoco-android:0.2'
        classpath "gradle.plugin.org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.12.0"
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.15.0'
        classpath 'gradle.plugin.com.github.sherter.google-java-format:google-java-format-gradle-plugin:0.9'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'com.hiya.jacoco-android'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.github.sherter.google-java-format'

jacocoAndroidUnitTestReport {
    csv.enabled false
    html.enabled true
    xml.enabled true
}

android {
    compileSdkVersion 31
    buildToolsVersion "31.0.0"

    defaultConfig {
        applicationId "edu.luc.etl.cs313.android.clickcounter"
        minSdkVersion 26
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }
    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.includeAndroidResources = true
    }
    sourceSets {
        final String sharedTestDir = 'src/sharedTest/java'
        test {
            java.srcDir sharedTestDir
        }
        androidTest {
            java.srcDir sharedTestDir
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'net.sourceforge.streamsupport:streamsupport:1.7.4'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.annotation:annotation:1.3.0'
    // some duplications on account of shared testcase superclass
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'androidx.test:rules:1.4.0'
    testImplementation 'org.robolectric:robolectric:4.8'
    testImplementation 'org.bouncycastle:bcprov-jdk15on:1.70'
    testImplementation 'androidx.appcompat:appcompat:1.4.1'
    androidTestImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.appcompat:appcompat:1.4.1'
    androidTestImplementation 'androidx.test:rules:1.4.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

afterEvaluate {
    javadoc.classpath += files(android.applicationVariants.collect { variant ->
        variant.javaCompileProvider.get().classpath.files
    })
}
